# change the line, VALUES (?scheme ?pid) { (<scheme> "pid") } to search for other PIDs
PREFIX gl: <http://schema.geolink.org/1.0/base/main#> 
SELECT DISTINCT ?publication ?role ?title ?alt_title ?pub_type 
WHERE { 
  ?person a gl:Person .             
  { SELECT DISTINCT ?person 
    WHERE { 
       { ?target owl:sameAs ?person . } 
       UNION { ?target owl:sameAs ?match . ?match owl:sameAs ?person . } 
       UNION { ?target owl:sameAs ?match . ?person owl:sameAs ?match . } 
       UNION { ?person a gl:Person . FILTER (?person = ?target) }
       VALUES (?scheme ?pid) { (<http://purl.spar.org/datacite/orcid> "0000-0002-6059-4651") }
       ?target gl:hasIdentifier ?identifier . 
       ?identifier rdf:type gl:Identifier . 
       ?identifier gl:hasIdentifierScheme ?scheme .
       ?identifier gl:hasIdentifierValue ?pid_value . 
       FILTER (str(?pid_value) = ?pid) 
    } 
  } 
  ?publication a gl:Document . 
  ?publication ?role ?person . 
  OPTIONAL { ?publication gl:hasTitle ?title . } 
  OPTIONAL { ?publication gl:title ?alt_title . } 
  OPTIONAL { ?publication gl:hasDocumentType ?pub_type . } 
 }